define

  ## PROPERTIES

  # migration properties
  "uuid" sub attribute datatype string;
  "title" sub attribute datatype string;
  "lastRun" sub attribute datatype string;
  "timestamp" sub attribute datatype long;

  # OpenCTI properties
  "created_at" sub attribute datatype date;
  "updated_at" sub attribute datatype date;
  "issuer" sub attribute datatype string;
  "duration" sub attribute datatype string;
  "password" sub attribute datatype string;
  "email" sub attribute datatype string;
  "firstname" sub attribute datatype string;
  "lastname" sub attribute datatype string;
  "language" sub attribute datatype string;
  "grant" sub attribute datatype string;
  "platform_title" sub attribute datatype string;
  "platform_language" sub attribute datatype string;
  "platform_email" sub attribute datatype string;
  "platform_url" sub attribute datatype string;
  "platform_external_auth" sub attribute datatype boolean;
  "platform_registration" sub attribute datatype boolean;
  "graph_data" sub attribute datatype string;

  # STIX common properties
  "stix_id" sub attribute datatype string;
  "stix_label" sub attribute datatype string;
  "stix_label_lowercase" sub attribute datatype string;
  "type" sub attribute datatype string;
  "created" sub attribute datatype date;
  "modified" sub attribute datatype date;
  "revoked" sub attribute datatype boolean;
  "first_seen" sub attribute datatype date;
  "first_seen_year" sub attribute datatype string;
  "first_seen_month" sub attribute datatype string;
  "last_seen" sub attribute datatype date;
  "last_seen_year" sub attribute datatype string;
  "last_seen_month" sub attribute datatype string;
  "name" sub attribute datatype string;
  "name_lowercase" sub attribute datatype string;
  "description" sub attribute datatype string;
  "description_lowercase" sub attribute datatype string;

  # STIX SDO
  "alias" sub attribute datatype string;
  "alias_lowercase" sub attribute datatype string;
  "stix_role" sub attribute datatype string;
  "objective" sub attribute datatype string;
  "goal" sub attribute datatype string;
  "sophistication" sub attribute datatype string;
  "resource_level" sub attribute datatype string;
  "primary_motivation" sub attribute datatype string;
  "secondary_motivation" sub attribute datatype string;
  "personal_motivation" sub attribute datatype string;
  "tool_version" sub attribute datatype string;
  "kill_chain_name" sub attribute datatype string;
  "phase_name" sub attribute datatype string;
  "phase_order" sub attribute datatype long;
  "definition_type" sub attribute datatype string;
  "definition" sub attribute datatype string;
  "level" sub attribute datatype long;
  "color" sub attribute datatype string;
  "source_name" sub attribute datatype string;
  "source_name_lowercase" sub attribute datatype string;
  "url" sub attribute datatype string;
  "hash" sub attribute datatype string;
  "external_id" sub attribute datatype string;
  "external_id_lowercase" sub attribute datatype string;
  "published" sub attribute datatype date;
  "published_year" sub attribute datatype string;
  "published_month" sub attribute datatype string;
  "report_class" sub attribute datatype string;
  "pattern" sub attribute datatype string;
  "valid_from" sub attribute datatype date;
  "valid_until" sub attribute datatype date;

  # STIX SRO
  "relationship_type" sub attribute datatype string;
  "weight" sub attribute datatype long;

  ## ROLES

  # Migration
  "status" sub role;
  "state" sub role;

  # OpenCTI roles
  "author" sub role;
  "authorization" sub role;
  "client" sub role;
  "allow" sub role;
  "allowed" sub role;
  "grouping" sub role;
  "member" sub role;

  # STIX common roles
  "so" sub role;
  "kill_chain_phase" sub role;

  # STIX embeded roles
  "creator" sub role;
  "marking" sub role;
  "external_reference" sub role;
  "phase_belonging" sub role;
  "knowledge_aggregation" sub role;

  # STIX SRO roles

  # targets
  "target" sub role;
  "source" sub role;

  # uses
  "usage" sub role;
  "user" sub role;

  # attributed-to
  "origin" sub role;
  "attribution" sub role;

  # mitigates
  "problem" sub role;
  "mitigation" sub role;

  # indicates
  "indicator" sub role;
  "characterize" sub role;

  # variant-of
  "original" sub role;
  "variation" sub role;

  # impersonates
  "genuine" sub role;
  "dummy" sub role;

  # related-to
  "relate_from" sub role;
  "relate_to" sub role;

  # localization
  "location" sub role;
  "localized" sub role;

  # belonging
  "gather" sub role;
  "part_of" sub role;

  ## RELATIONSHIPS

  # Login relationship
  "authorize" sub relationship
    relates "authorization"
    relates "client";

  # Migration relationships
  "migrate" sub relationship
    relates "status"
    relates "state";

  # Group membership relationships
  "membership" sub relationship
    relates "grouping"
    relates "member";

  # Access relationships
  "permission" sub relationship
    has "grant"
    relates "allow"
    relates "allowed";

  "user_permission" sub relationship
    has "grant"
    relates "allow"
    relates "allowed";

  # OpenCTI embeded relationships
  "relation_embedded" sub relationship
    is-abstract;

  "authored_by" sub "relation_embedded"
    relates "author"
    relates "so";

  "owned_by" sub "relation_embedded"
    relates "owner"
    relates "to";

  # STIX embeded relationships
  "stix_relation_embedded" sub relationship
    is-abstract
    has "stix_id";

  "created_by_ref" sub "stix_relation_embedded"
    relates "creator"
    relates "so";

  "object_marking_refs" sub "stix_relation_embedded"
    relates "marking"
    relates "so";

  "external_references" sub "stix_relation_embedded"
    relates "external_reference"
    relates "so";

  "kill_chain_phases" sub "stix_relation_embedded"
    relates "phase_belonging"
    relates "kill_chain_phase";

  "object_refs" sub "stix_relation_embedded"
    relates "so"
    relates "knowledge_aggregation";

  # STIX SRO relationships
  "stix_relation" sub relationship
    is-abstract
    has "stix_id"
    has "created"
    has "modified"
    has "revoked"
    has "type"
    has "relationship_type"
    has "weight"
    has "first_seen"
    has "first_seen_month"
    has "first_seen_year"
    has "last_seen"
    has "last_seen_month"
    has "last_seen_year"
    has "name"
    has "name_lowercase"
    has "description"
    has "description_lowercase"
    has "created_at"
    has "updated_at"
    plays "so";

  # targets
  "targets" sub "stix_relation"
    relates "target"
    relates "source";

  # uses
  "uses" sub "stix_relation"
    relates "usage"
    relates "user";

  # related-to
  "related-to" sub "stix_relation"
    relates "relate_from"
    relates "relate_to";

  # mitigates
  "mitigates" sub "stix_relation"
    relates "problem"
    relates "mitigation";

  # indicates
  "indicates" sub "stix_relation"
    relates "characterize"
    relates "indicator";

  # impersonates
  "impersonates" sub "stix_relation"
    relates "genuine"
    relates "dummy";

  # parentship
  "parentship" sub "stix_relation"
    is-abstract;

  # attributed-to
  "attributed-to" sub "parentship"
    relates "origin"
    relates "attribution";

  # variant-of
  "variant-of" sub "parentship"
    relates "original"
    relates "variation";

  # localization
  "localization" sub "parentship"
    relates "location"
    relates "localized";

  # belonging
  "gathering" sub "parentship"
    relates "gather"
    relates "part_of";

  ## ENTITIES
  # Migration entities
  "MigrationStatus" sub entity
    has "lastRun"
    plays "status";

  "MigrationReference" sub entity
    has "title"
    has "timestamp"
    plays "state";

  # OpenCTI entities
  "Settings" sub entity
    has "type"
    has "platform_title"
    has "platform_email"
    has "platform_url"
    has "platform_language"
    has "platform_external_auth"
    has "platform_registration"
    has "created_at"
    has "updated_at";

  "Group" sub entity
    has "type"
    has "name"
    has "name_lowercase"
    has "description"
    has "description_lowercase"
    has "created_at"
    has "updated_at"
    plays "allowed"
    plays "grouping";

  "Workspace" sub entity
    has "type"
    has "name"
    has "name_lowercase"
    has "description"
    has "description_lowercase"
    has "graph_data"
    has "created_at"
    has "updated_at"
    plays "to"
    plays "so"
    plays "knowledge_aggregation";

  "Token" sub entity
    has "uuid"
    has "type"
    has "name"
    has "revoked"
    has "created"
    has "duration"
    has "issuer"
    has "created_at"
    has "updated_at"
    plays "authorization";

  "Stix-Domain" sub entity
    is-abstract
    has "stix_id"
    has "stix_label"
    has "stix_label_lowercase"
    has "type"
    has "created"
    has "modified"
    has "revoked"
    has "created_at"
    has "updated_at"
    plays "relate_from"
    plays "relate_to"
    plays "so";

  "Stix-Domain-Entity" sub "Stix-Domain"
    is-abstract
    has "name"
    has "name_lowercase"
    has "description"
    has "description_lowercase"
    has "alias"
    has "alias_lowercase"
    has "graph_data";

  "External-Reference" sub "Stix-Domain"
    has "source_name"
    has "source_name_lowercase"
    has "description"
    has "description_lowercase"
    has "url"
    has "hash"
    has "external_id"
    has "external_id_lowercase"
    plays "external_reference";

  "Kill-Chain-Phase" sub "Stix-Domain"
    has "kill_chain_name"
    has "phase_name"
    has "phase_order"
    plays "kill_chain_phase";

  "Marking-Definition" sub "Stix-Domain"
    has "definition_type"
    has "definition"
    has "level"
    has "color"
    plays "marking"
    plays "allow";

  "Attack-Pattern" sub "Stix-Domain-Entity"
    plays "phase_belonging"
    plays "source"
    plays "user"
    plays "usage"
    plays "problem"
    plays "characterize";

  "Campaign" sub "Stix-Domain-Entity"
    has "first_seen"
    has "first_seen_month"
    has "first_seen_year"
    has "last_seen"
    has "last_seen_month"
    has "last_seen_year"
    has "objective"
    plays "attribution"
    plays "origin"
    plays "user"
    plays "source"
    plays "characterize";

  "Course-Of-Action" sub "Stix-Domain-Entity"
    plays "mitigation";

  "Identity" sub "Stix-Domain-Entity"
    is-abstract
    plays "creator"
    plays "target"
    plays "origin"
    plays "dummy"
    plays "user"
    plays "genuine";

  "Country" sub "Identity"
    plays "location";

  "City" sub "Identity"
    plays "localized"
    plays "location";

  "Sector" sub "Identity"
    plays "gather";

  "Organization" sub "Identity"
    plays "localized"
    plays "gather";

  "Threat-Actor" sub "Identity"
    has "stix_role"
    has "goal"
    has "sophistication"
    has "resource_level"
    has "primary_motivation"
    has "secondary_motivation"
    has "personal_motivation"
    plays "attribution"
    plays "source"
    plays "user"
    plays "characterize"
    plays "part_of";

  "User" sub "Identity"
    has "password"
    has "email"
    has "firstname"
    has "lastname"
    has "language"
    has "grant" # roles is a reserved word
    plays "client"
    plays "author"
    plays "owner"
    plays "member"
    plays "allowed"
    plays "part_of";

  "Indicator" sub "Stix-Domain-Entity"
    has "pattern"
    has "valid_from"
    has "valid_until"
    plays "phase_belonging"
    plays "indicator";

  "Incident" sub "Stix-Domain-Entity"
    has "first_seen"
    has "first_seen_month"
    has "first_seen_year"
    has "last_seen"
    has "last_seen_month"
    has "last_seen_year"
    plays "attribution"
    plays "user"
    plays "usage"
    plays "source"
    plays "characterize";

  "Intrusion-Set" sub "Stix-Domain-Entity"
    has "first_seen"
    has "first_seen_month"
    has "first_seen_year"
    has "last_seen"
    has "last_seen_month"
    has "last_seen_year"
    has "goal"
    has "resource_level"
    has "primary_motivation"
    has "secondary_motivation"
    plays "attribution"
    plays "source"
    plays "user"
    plays "origin"
    plays "characterize";

  "Malware" sub "Stix-Domain-Entity"
    plays "phase_belonging"
    plays "source"
    plays "user"
    plays "variation"
    plays "usage"
    plays "problem"
    plays "characterize"
    plays "original";

  "Report" sub "Stix-Domain-Entity"
    has "published"
    has "published_year"
    has "published_month"
    has "report_class"
    plays "knowledge_aggregation";

  "Tool" sub "Stix-Domain-Entity"
    has "tool_version"
    plays "phase_belonging"
    plays "source"
    plays "user"
    plays "usage"
    plays "problem"
    plays "characterize";

  "Vulnerability" sub "Stix-Domain-Entity"
    plays "target"
    plays "problem";

    ## RULES
    # Access rule
    "UserPermissionRule" sub rule
    when {
      ("member": $user, "grouping": $group) isa membership;
      ("allowed": $group, "allow": $marking) isa permission;
      $marking isa "Marking-Definition" has "level" $markingLevel has "definition_type" $markingType;
      $markingDefinition isa "Marking-Definition" has "level" $markingDefinitionLevel has "definition_type" $markingDefinitionType;
      $markingDefinitionType == $markingType;
      $markingDefinitionLevel <= $markingLevel;
    } then {
      ("allowed": $user, "allow": $markingDefinition) isa "user_permission";
    };

    # Threat Actors => Malwares rule
    "ThreatActorUsesMalwareRule" sub rule
    when {
      $intrusionSet isa "Intrusion-Set";
      $malware isa "Malware";
      $threatActor isa "Threat-Actor";
      ("user": $intrusionSet, "usage": $malware) isa uses;
      ("origin": $threatActor, "attribution": $intrusionSet) isa attributed-to;
    } then {
      ("user": $threatActor, "usage": $malware) isa "uses";
    };